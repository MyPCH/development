local kube = import 'kube.jsonnet';

local ClusterConfig(config) = kube.kubeobj('eksctl.io/v1alpha5', 'ClusterConfig', config.cluster.name) {
  local cluster = config.cluster,
  local eks = cluster.aws.eks,
  metadata+: {
    region: cluster.aws.region,
    version: cluster.k8sVersion,
  },
  vpc: {
    cidr: eks.cidr,
  },
  nodeGroups: [
    {
      name: 'ng-1',
      instanceType: eks.nodegroup.instanceType,
      desiredCapacity: eks.nodegroup.desiredCapacity,
      minSize: eks.nodegroup.minSize,
      maxSize: eks.nodegroup.maxSize,
      labels: {
        'kiam-server': 'false',
      },
      tags: {
        'k8s.io/cluster-autoscaler/enabled': 'true',
        ['k8s.io/cluster-autoscaler/' + cluster.name]: 'true',
      },
    },
    {
      name: 'ng-kiam',
      instanceType: eks.nodegroup.instanceType,
      desiredCapacity: 1,
      labels: {
        'kiam-server': 'true',
      },
      taints: {
        'kiam-server': 'false:NoExecute',
      },
    },
  ],
};

function(config) ClusterConfig(config)
