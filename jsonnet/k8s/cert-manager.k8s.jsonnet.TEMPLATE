local aws = import 'aws.jsonnet';
local kube = import 'kube.jsonnet';

local ClusterIssuer(config, group, server, name) = {
  apiVersion: 'certmanager.k8s.io/v1alpha1',
  kind: 'ClusterIssuer',
  metadata: {
    name: name,
    namespace: group.namespace.name,
  },
  spec: {
    acme: {
      server: server,
      email: config.company.email,
      privateKeySecretRef: {
        name: name,
      },
      solvers: [
        {
          dns01: {
            route53: {
              region: config.cluster.aws.region,
            },
          },
        },
      ],
    },
  },
};

function(config) (
  local group = config.groups.certManager;
  if group.enabled then {
    Helmrelease: if group.helmrelease.create then aws.helmrelease(config, group),
    Namespace: if group.namespace.create then aws.namespace(config, group),
    StagingClusterIssuer:
      ClusterIssuer(config,
                    group,
                    'https://acme-staging-v02.api.letsencrypt.org/directory',
                    'letsencrypt-staging'),
    ProductionClusterIssuer:
      ClusterIssuer(config,
                    group,
                    'https://acme-v02.api.letsencrypt.org/directory',
                    'letsencrypt-production'),
  }
)
