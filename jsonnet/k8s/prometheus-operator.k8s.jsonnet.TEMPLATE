local aws = import 'aws.jsonnet';

local Helmrelease(config, group) = aws.helmrelease(config, group) {
  local thanos = config.groups.thanos,

  spec+: {
    values+: {
      prometheus+: {
        prometheusSpec+: {
          externalLabels: {  // a cool way to add default labels to all metrics
            geo: 'us',
            region: config.cluster.aws.region,
          },
          serviceMonitorNamespaceSelector: {  // allows the operator to find target config from multiple namespaces
            any: true,
          },
          thanos: {  // add Thanos Sidecar
            tag: thanos.deployment.tag,  // a specific version of Thanos
            objectStorageConfig: {  // blob storage configuration to upload metrics
              key: 'thanos.yaml',
              name: thanos.secret.name,
            },
          },
        },
      },
      grafana: {  // (optional) we don't need Grafana in all clusters
        enabled: false,
      },
    },
  },
};

function(config) (
  local group = config.groups.prometheusOperator;
  if group.enabled then {
    Helmrelease: Helmrelease(config, group),
    Namespace: if group.namespace.create then aws.namespace(config, group),
  }
)
