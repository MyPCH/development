local aws = import 'aws.jsonnet';
local obj = import 'obj.jsonnet';

local IamMangedPolicy(config, env) = aws.iamManagedPolicy(config, env) {
  values+:: {
    Properties+: {
      Statements: [
        {
          Action: 's3:ListBucket',
          Resource: ['arn:aws:s3:::%s/*' % aws.bucketName(config, env)],
          Effect: 'Allow',
        },
        {
          Action: [
            's3:GetObject',
            's3:PutObject',
            's3:DeleteObject',
          ],
          Resource: ['arn:aws:s3:::%s/*' % aws.bucketName(config, env)],
          Effect: 'Allow',
        },
        {
          Effect: 'Allow',
          Action: 'ses:*',
          Resource: '*',
        },
      ],
    },
  },
};

function(config) (
  local iamManagedPolicy(name, env) = if env.enabled then IamMangedPolicy(config, env { name: name });
  local iamManagedPolicies = std.mapWithKey(iamManagedPolicy, config.tidepool.groups);

  local iamRole(name, env) = if env.enabled then aws.iamRole(config, env { name: name });
  local iamRoles = std.mapWithKey(iamRole, config.tidepool.groups);

  obj.values(std.prune(iamManagedPolicies)) + obj.values(std.prune(iamRoles))
)
